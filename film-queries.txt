http://dbpedia.org/sparql

1. Finn fornavn på alle som studerer informatikk.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>

SELECT ?fornavn  ?etternavn WHERE {
  ?person cx:studieretning "Informatikk" .
  ?person foaf:firstName ?fornavn .
  ?person foaf:lastName  ?etternavn .
} 

2. Finner alle personer som kjenner noen som er 22 år gamle.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>

SELECT ?person1 WHERE {
  ?person1 foaf:knows ?person2 .
  ?person2 cx:alder 27 .
} 

3. Finner fornavn og filmtittel til alle som har sett en film med strengen "Godfather i tittlen" .

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>

SELECT ?fornavn ?tittel WHERE {
  ?person cx:harSett ?movie .
  ?person foaf:firstName ?fornavn .
  ?movie rdfs:label ?tittel .
  FILTER (regex(?tittel,"Godfather","i"))    
} 

4. Finner navn på skuespillere en gitt person har sett.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT DISTINCT ?actorName WHERE {
  <http://semweb.computas.com/service/uib/person/3> cx:harSett ?film .	
  SERVICE <http://dbpedia.org/sparql> {
    ?film dbpedia-owl:starring ?actor .
    ?actor foaf:name ?actorName .
  }
} 
LIMIT 20

5. Fornavn, Regissør, Film (bare filmer siden dbpedia-owl:director har domain dbpedia-owl:Film)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?name ?director ?film WHERE {
  ?person foaf:firstName ?name . 
  ?person cx:harSett ?f .
  ?f rdfs:label ?film .    
  SERVICE <http://dbpedia.org/sparql> {
    ?f dbpedia-owl:director ?d .
    ?d rdfs:label ?director .
    FILTER(lang(?director)="en")
  }
}
LIMIT 20 

6. Fornavn, Skuespiller, Film (både filmer og serie siden dbpedia-owl:starring har domain dbpedia-owl:Work)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT DISTINCT ?name ?actor ?film WHERE {
  ?person foaf:firstName ?name . 
  ?person cx:harSett ?f .
  ?f rdfs:label ?film .    
  SERVICE <http://dbpedia.org/sparql> {
    ?f dbpedia-owl:starring ?d .
    ?d rdfs:label ?actor .
    FILTER(lang(?actor)="en")
  }
}
LIMIT 20 

7. Hvem har sett filmer med både Matt Damon og Ben Affleck

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cx: <http://semweb.computas.com/service/ontologi#>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT DISTINCT ?name ?film WHERE {
  ?person foaf:firstName ?name . 
  ?person cx:harSett ?f .
  ?f rdfs:label ?film .    
  SERVICE <http://dbpedia.org/sparql> {
    ?f dbpedia-owl:starring ?d1 .
    ?f dbpedia-owl:starring ?d2 .
    ?d1 rdfs:label ?actor1 .
    ?d2 rdfs:label ?actor2 .		
    FILTER(regex(?actor1,"Ben Affleck","i"))
    FILTER(regex(?actor2,"Matt Damon","i"))
  }
}
LIMIT 20 